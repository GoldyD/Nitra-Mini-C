using Nitra;
using Nitra.Runtime;
using Nitra.Declarations;

namespace MiniC 
{
  map syntax MiniCSyntax.CompilationUnit -> CompilationUnit
  {
    TopDeclarations -> TopDeclarations;
  }
  
  map syntax MiniCSyntax.TopDeclaration -> TopDeclaration
  {
    | VarDeclaration -> VarDeclaration 
    | FunDeclaration -> FunDeclaration 
      {
        Name                  -> Name;
        TypeRef               -> ReturnTypeRef;
        VarDeclarations.Item1 -> Parameters;
        CompoundStatement     -> CompoundStatement;
      }
  }
  
  map syntax MiniCSyntax.CompoundStatement -> CompoundStatement
  {
    VarDeclarations -> LocalVariables;
  }

  map syntax MiniCSyntax.VarDeclaration -> VarDeclaration
  {
    | Scalar -> ScalarDeclaration
      {
        Name    -> Name;
        TypeRef -> TypeRef;
      }
  
    | Array -> ArrayDeclaration
      {
        Name    -> Name;
        TypeRef -> TypeRef;
      }
  }
  
  map syntax MiniCSyntax.TypeRef -> TypeReference
  {
    | Void  -> Void  {}
    | Int   -> Int   {}
    | Float -> Float {}
    | Bool  -> Bool  {}
  }
}