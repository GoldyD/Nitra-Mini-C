using MiniC;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Declarations;
using Nitra.ProjectSystem;

using System;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Console;
using System.IO;
using System.Linq;
using System.Threading;

namespace MiniC.Compiler
{
  module Program
  {
    Main() : void
    {
      def config = Config();
      when (config.Success)
      {
        def solution = GenerateSolution(config);
        RefreshSolution(solution);
        mutable hasError = false;
        foreach(project in solution.Projects)
          foreach(file in project.Files)
            foreach (msg in file.GetCompilerMessages())
            {
              hasError = true;
              WriteLine(msg);
            }
          
        when (hasError)
          return;

        foreach(project in solution.Projects)
          DotNetBackend.GenerateAssembly(project, config.OutputPath);
      }
    }
        
    GenerateSolution(cfg: Config): FsSolution[IAst]
    {
      def solution = FsSolution();
      _ = FsProject(solution, Path.GetDirectoryName(cfg.FileName), [FsFile(cfg.FileName, MiniC.Instance)], []);
      solution
    }
    
    public RefreshSolution(solution : Solution): void
    {
      foreach(project in solution.Projects)
      {
        def projectSupport   = project.GetProjectSupport();
        def data             = projectSupport.RefreshReferences(CancellationToken.None, project);
        def files            = project.Files.Select(_.GetEvalPropertiesData()).ToImmutableArray();
        projectSupport.RefreshProject(CancellationToken.None, files, data);
      }
    }
  }
}