using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using System;
using System.Console;
using System.IO;
using System.Linq;
using System.Collections.Generic;
using Nitra.Declarations;
using Nitra.ProjectSystem;
using MiniC;

namespace MiniC.Compiler
{
  module Program
  {
    Main() : void
    {
      def config = CommandPromptReader();
      when (config.Success)
      {
        def solution = GenerateSolution(config);
        RefreshSolution(solution);
        mutable hasError = false;
        foreach(project in solution.Projects)
          foreach(file in project.Files)
            foreach (msg in file.GetCompilerMessages())
            {
              hasError = true;
              WriteLine(msg);
            }
          
        unless (hasError)
          foreach(project in solution.Projects)
            foreach(file in project.Files)
              DotNetBackend.GenerateAssembly(file, config.OutputPath);
        
      }
    }
        
    GenerateSolution(cfg: CommandPromptReader): FsSolution[IAst]
    {
      def solution = FsSolution();
      _ = FsProject(solution, Path.GetDirectoryName(cfg.FileName), [FsFile(cfg.FileName, MiniC.Instance)], []);
      solution
    }
    
    public RefreshSolution(solution: Solution): void
    {
      foreach(project in solution.Projects)
        Evaluator.RefreshProject(project);
    }
  }
}